#Область ПрограммныйИнтерфейс

Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	Результат  = Новый ТабличныйДокумент;
	Макет      = ПолучитьМакет("ТоварыПоКонтрагенту");
	ВывестиТаблицу(ПараметрыОтчета, Макет, Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыводТаблицыОбщий

Процедура ВывестиТаблицу(ПараметрыОтчета, Макет, Результат)
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	//ПараметрыШапки = Новый Структура;
	//ПараметрыШапки.Вставить("Контрагент", ПараметрыОтчета.Контрагент);
	ОбластьШапка.Параметры.Заполнить(ПараметрыОтчета);
	Результат.Вывести(ОбластьШапка);
	
	ОбластьСтрокиТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ИтогоЦенаИзделия = 0;
	НомерПоПорядку   = 0;
	Отступ           = 0;
	
	ДанныеПоДокументам = ДанныеПоДокументам(ПараметрыОтчета);
	
	Для Каждого Строка Из ДанныеПоДокументам Цикл
		
		НомерПоПорядку = НомерПоПорядку + 1;
		ДанныеПоСтроке = НовыйДанныеПоСтроке();
		ЗаполнитьЗначенияСвойств(ДанныеПоСтроке, Строка);
		ДанныеПоСтроке.НомерПоПорядку = НомерПоПорядку;
		
		ОбластьСтрокиТаблицы.Параметры.Заполнить(ДанныеПоСтроке);
		ОбластьСтрокиТаблицы.Области.Номенклатура.Отступ = Отступ;
		Результат.Вывести(ОбластьСтрокиТаблицы);
		
	КонецЦикла;
	
	// Выведем итог таблицы
	ПараметрыИтогов = Новый Структура;
	ПараметрыИтогов.Вставить("Количество", ДанныеПоДокументам.Итог("Количество"));
	ПараметрыИтогов.Вставить("СуммаНДС",   ДанныеПоДокументам.Итог("СуммаНДС"));
	ПараметрыИтогов.Вставить("Сумма",      ДанныеПоДокументам.Итог("Сумма"));
	ОбластьИтогоТаблицы = Макет.ПолучитьОбласть("ИтогТаблицы");
	ОбластьИтогоТаблицы.Параметры.Заполнить(ПараметрыИтогов);
	Результат.Вывести(ОбластьИтогоТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеДанныхИБ

Функция ДанныеПоДокументам(ПараметрыОтчета)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("Контрагент",    ПараметрыОтчета.Контрагент);
	Запрос.УстановитьПараметр("Договор",       ПараметрыОтчета.Договор);
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоДня(ПараметрыОтчета.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПараметрыОтчета.ДатаОкончания));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен = ИСТИНА
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента = &Договор
	|	И РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка
	|ПОМЕСТИТЬ ВТ_Возвраты
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Проведен = ИСТИНА
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И ВозвратТоваровОтПокупателя.Контрагент = &Контрагент
	|	И ВозвратТоваровОтПокупателя.ДоговорКонтрагента = &Договор
	|	И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество,
	|	РеализацияТоваровУслугТовары.Цена,
	|	СУММА(РеализацияТоваровУслугТовары.Сумма) КАК Сумма,
	|	РеализацияТоваровУслугТовары.СтавкаНДС,
	|	СУММА(РеализацияТоваровУслугТовары.СуммаНДС) КАК СуммаНДС,
	|	РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС,
	|	РеализацияТоваровУслугТовары.Номенклатура.Код КАК Код,
	|	РеализацияТоваровУслугТовары.Номенклатура.Артикул КАК Артикул
	|ПОМЕСТИТЬ ДанныеПоРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Реализации.Ссылка
	|			ИЗ
	|				ВТ_Реализации КАК ВТ_Реализации)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслугТовары.СтавкаНДС,
	|	РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС,
	|	РеализацияТоваровУслугТовары.Цена,
	|	РеализацияТоваровУслугТовары.Номенклатура.Код,
	|	РеализацияТоваровУслугТовары.Номенклатура.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	СУММА(-ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	|	ВозвратТоваровОтПокупателяТовары.Цена,
	|	СУММА(-ВозвратТоваровОтПокупателяТовары.Сумма) КАК Сумма,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
	|	СУММА(-ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Код КАК Код,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Артикул КАК Артикул
	|ПОМЕСТИТЬ ДанныеПоВозвратам
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Возвраты.Ссылка
	|			ИЗ
	|				ВТ_Возвраты КАК ВТ_Возвраты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС,
	|	ВозвратТоваровОтПокупателяТовары.Цена,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Код,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоРеализации.Номенклатура,
	|	ДанныеПоРеализации.Количество,
	|	ДанныеПоРеализации.Цена,
	|	ДанныеПоРеализации.Сумма,
	|	ДанныеПоРеализации.СтавкаНДС,
	|	ДанныеПоРеализации.СуммаНДС,
	|	ДанныеПоРеализации.СуммаВключаетНДС,
	|	ДанныеПоРеализации.Код,
	|	ДанныеПоРеализации.Артикул
	|ИЗ
	|	ДанныеПоРеализации КАК ДанныеПоРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеПоВозвратам.Номенклатура,
	|	ДанныеПоВозвратам.Количество,
	|	ДанныеПоВозвратам.Цена,
	|	ДанныеПоВозвратам.Сумма,
	|	ДанныеПоВозвратам.СтавкаНДС,
	|	ДанныеПоВозвратам.СуммаНДС,
	|	ДанныеПоВозвратам.СуммаВключаетНДС,
	|	ДанныеПоВозвратам.Код,
	|	ДанныеПоВозвратам.Артикул
	|ИЗ
	|	ДанныеПоВозвратам КАК ДанныеПоВозвратам";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область Конструкторы

Функция НовыйДанныеПоСтроке()
	
	Возврат Новый Структура(
		"НомерПоПорядку,
		|Номенклатура,
		|Код,
		|Артикул,
		|Количество,
		|Цена,
		|СтавкаНДС,
		|СуммаНДС,
		|Сумма");
		
КонецФункции

#КонецОбласти
