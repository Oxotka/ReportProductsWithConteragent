#Область ПрограммныйИнтерфейс

Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	Результат  = Новый ТабличныйДокумент;
	Макет      = ПолучитьМакет("ТоварыПоКонтрагенту");
	ВывестиТаблицу(ПараметрыОтчета, Макет, Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыводТаблицыОбщий

Процедура ВывестиТаблицу(ПараметрыОтчета, Макет, Результат)
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	//ПараметрыШапки = Новый Структура;
	//ПараметрыШапки.Вставить("Контрагент", ПараметрыОтчета.Контрагент);
	ОбластьШапка.Параметры.Заполнить(ПараметрыОтчета);
	Результат.Вывести(ОбластьШапка);
	
	ОбластьСтрокиТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ИтогоЦенаИзделия = 0;
	НомерПоПорядку   = 0;
	Отступ           = 0;
	
	ДанныеПоДокументам = ДанныеПоДокументам(ПараметрыОтчета);
	
	Для Каждого Строка Из ДанныеПоДокументам Цикл
		
		НомерПоПорядку = НомерПоПорядку + 1;
		ДанныеПоСтроке = НовыйДанныеПоСтроке();
		ЗаполнитьЗначенияСвойств(ДанныеПоСтроке, Строка);
		ДанныеПоСтроке.НомерПоПорядку = НомерПоПорядку;
		
		ОбластьСтрокиТаблицы.Параметры.Заполнить(ДанныеПоСтроке);
		ОбластьСтрокиТаблицы.Области.Номенклатура.Отступ = Отступ;
		Результат.Вывести(ОбластьСтрокиТаблицы);
		
	КонецЦикла;
	
	// Выведем итог таблицы
	ПараметрыИтогов = Новый Структура;
	ПараметрыИтогов.Вставить("Количество", ДанныеПоДокументам.Итог("Количество"));
	ПараметрыИтогов.Вставить("СуммаНДС",   ДанныеПоДокументам.Итог("СуммаНДС"));
	ПараметрыИтогов.Вставить("Сумма",      ДанныеПоДокументам.Итог("Сумма"));
	ОбластьИтогоТаблицы = Макет.ПолучитьОбласть("ИтогТаблицы");
	ОбластьИтогоТаблицы.Параметры.Заполнить(ПараметрыИтогов);
	Результат.Вывести(ОбластьИтогоТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеДанныхИБ

Функция ДанныеПоДокументам(ПараметрыОтчета)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("Контрагент",    ПараметрыОтчета.Контрагент);
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоДня(ПараметрыОтчета.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПараметрыОтчета.ДатаОкончания));
	ТекстЗапроса = ТекстЗапросаПоДокументам();
	Если ПараметрыОтчета.ВидКонтрагента = "Поставщик" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РеализацияТоваровУслуг", "ПоступлениеТоваровУслуг");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВозвратТоваровОтПокупателя", "ВозвратТоваровПоставщику");
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.Договор) Тогда
		Запрос.УстановитьПараметр("Договор",       ПараметрыОтчета.Договор);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ОсновнойДокумент.ДоговорКонтрагента = &Договор", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ДокументВозврата.ДоговорКонтрагента = &Договор", "");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ТекстЗапросаПоДокументам()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОсновнойДокумент.Ссылка
	|ПОМЕСТИТЬ ВТ_ОсновныеДокументы
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ОсновнойДокумент
	|ГДЕ
	|	ОсновнойДокумент.Проведен = ИСТИНА
	|	И ОсновнойДокумент.Организация = &Организация
	|	И ОсновнойДокумент.Контрагент = &Контрагент
	|	И ОсновнойДокумент.ДоговорКонтрагента = &Договор
	|	И ОсновнойДокумент.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументВозврата.Ссылка
	|ПОМЕСТИТЬ ВТ_Возвраты
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ДокументВозврата
	|ГДЕ
	|	ДокументВозврата.Проведен = ИСТИНА
	|	И ДокументВозврата.Организация = &Организация
	|	И ДокументВозврата.Контрагент = &Контрагент
	|	И ДокументВозврата.ДоговорКонтрагента = &Договор
	|	И ДокументВозврата.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнойДокументТовары.Номенклатура,
	|	СУММА(ОсновнойДокументТовары.Количество) КАК Количество,
	|	ОсновнойДокументТовары.Цена,
	|	СУММА(ОсновнойДокументТовары.Сумма) КАК Сумма,
	|	ОсновнойДокументТовары.СтавкаНДС,
	|	СУММА(ОсновнойДокументТовары.СуммаНДС) КАК СуммаНДС,
	|	ОсновнойДокументТовары.Номенклатура.Код КАК Код,
	|	ОсновнойДокументТовары.Номенклатура.Артикул КАК Артикул
	|ПОМЕСТИТЬ ДанныеПоОсновнымДокументам
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ОсновнойДокументТовары
	|ГДЕ
	|	ОсновнойДокументТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ОсновныеДокументы.Ссылка
	|			ИЗ
	|				ВТ_ОсновныеДокументы КАК ВТ_ОсновныеДокументы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнойДокументТовары.Номенклатура,
	|	ОсновнойДокументТовары.СтавкаНДС,
	|	ОсновнойДокументТовары.Цена,
	|	ОсновнойДокументТовары.Номенклатура.Код,
	|	ОсновнойДокументТовары.Номенклатура.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТовары.Номенклатура,
	|	СУММА(-ВозвратТовары.Количество) КАК Количество,
	|	ВозвратТовары.Цена,
	|	СУММА(-ВозвратТовары.Сумма) КАК Сумма,
	|	ВозвратТовары.СтавкаНДС,
	|	СУММА(-ВозвратТовары.СуммаНДС) КАК СуммаНДС,
	|	ВозвратТовары.Номенклатура.Код КАК Код,
	|	ВозвратТовары.Номенклатура.Артикул КАК Артикул
	|ПОМЕСТИТЬ ДанныеПоВозвратам
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТовары
	|ГДЕ
	|	ВозвратТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_Возвраты.Ссылка
	|			ИЗ
	|				ВТ_Возвраты КАК ВТ_Возвраты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТовары.Номенклатура,
	|	ВозвратТовары.СтавкаНДС,
	|	ВозвратТовары.Цена,
	|	ВозвратТовары.Номенклатура.Код,
	|	ВозвратТовары.Номенклатура.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоОсновнымДокументам.Номенклатура КАК Номенклатура,
	|	СУММА(ДанныеПоОсновнымДокументам.Количество) КАК Количество,
	|	ДанныеПоОсновнымДокументам.Цена,
	|	СУММА(ДанныеПоОсновнымДокументам.Сумма) КАК Сумма,
	|	ДанныеПоОсновнымДокументам.СтавкаНДС,
	|	СУММА(ДанныеПоОсновнымДокументам.СуммаНДС) КАК СуммаНДС,
	|	ДанныеПоОсновнымДокументам.Код,
	|	ДанныеПоОсновнымДокументам.Артикул
	|ПОМЕСТИТЬ ВсеДокументы
	|ИЗ
	|	ДанныеПоОсновнымДокументам КАК ДанныеПоОсновнымДокументам
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоОсновнымДокументам.Номенклатура,
	|	ДанныеПоОсновнымДокументам.СтавкаНДС,
	|	ДанныеПоОсновнымДокументам.Код,
	|	ДанныеПоОсновнымДокументам.Артикул,
	|	ДанныеПоОсновнымДокументам.Цена
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеПоВозвратам.Номенклатура,
	|	ДанныеПоВозвратам.Количество,
	|	ДанныеПоВозвратам.Цена,
	|	ДанныеПоВозвратам.Сумма,
	|	ДанныеПоВозвратам.СтавкаНДС,
	|	ДанныеПоВозвратам.СуммаНДС,
	|	ДанныеПоВозвратам.Код,
	|	ДанныеПоВозвратам.Артикул
	|ИЗ
	|	ДанныеПоВозвратам КАК ДанныеПоВозвратам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДокументы.Номенклатура КАК Номенклатура,
	|	ВсеДокументы.Количество,
	|	ВсеДокументы.Цена,
	|	ВсеДокументы.Сумма,
	|	ВсеДокументы.СтавкаНДС,
	|	ВсеДокументы.СуммаНДС,
	|	ВсеДокументы.Код,
	|	ВсеДокументы.Артикул
	|ИЗ
	|	ВсеДокументы КАК ВсеДокументы
	|ГДЕ
	|	ВсеДокументы.Количество > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Конструкторы

Функция НовыйДанныеПоСтроке()
	
	Возврат Новый Структура(
		"НомерПоПорядку,
		|Номенклатура,
		|Код,
		|Артикул,
		|Количество,
		|Цена,
		|СтавкаНДС,
		|СуммаНДС,
		|Сумма");
		
КонецФункции

#КонецОбласти
